<?xml version="1.0" encoding="utf-8"?>
<s:SparkSkin xmlns:fx="http://ns.adobe.com/mxml/2009"
             xmlns:s="library://ns.adobe.com/flex/spark"
             xmlns:mx="library://ns.adobe.com/flex/mx">
    <!-- host component -->
    <fx:Metadata>
        [HostComponent("widgets.Chart.ChartBrowser")]
    </fx:Metadata>

    <fx:Script>
        <![CDATA[
            import com.esri.ags.utils.StringUtil;
            import com.esri.ags.portal.supportClasses.PopUpMediaInfo;

            import mx.core.IVisualElement;

            /* Define the symbol fill items that should be colored by the "symbolColor" style.*/
            private static const symbols:Array = [ "borderRectSymbol" ];

            /**
             * @private
             */
            override public function get symbolItems():Array
            {
                return symbols;
            }

            override protected function commitProperties():void
            {
                super.commitProperties();

                var attributes:Object = hostComponent.attributes;
                var formattedAttributes:Object = hostComponent.formattedAttributes;
                var mediaInfo:PopUpMediaInfo = hostComponent.activeMediaInfo;

                hideAllElementsExceptBorder();

                if (mediaInfo)
                {
                    showChart(mediaInfo);

                    // show title
                    titleLabel.text = StringUtil.substitute(mediaInfo.title, formattedAttributes);

                    showOrHideCount();

                    // show caption
                    var captionText:String = insertStats(mediaInfo.caption);
                    captionLabel.text = StringUtil.substitute(captionText, formattedAttributes);
                }
            }

            private function hideAllElementsExceptBorder():void
            {
                for (var i:int = 0; i < chartGroup.numElements; i++)
                {
                    var element:IVisualElement = chartGroup.getElementAt(i);
                    if (element !== borderRect)
                    {
                        hideElement(element);
                    }
                }
            }

            private function hideElement(element:IVisualElement):void
            {
                if (element.visible)
                {
                    element.visible = false;
                    element.includeInLayout = false;
                }
            }

            private function showElement(element:IVisualElement):void
            {
                if (!element.visible)
                {
                    element.visible = true;
                    element.includeInLayout = true;
                }
            }

            private function showChart(mediaInfo:PopUpMediaInfo):void
            {
                var chartData:Array = hostComponent.getChartData();
                if (mediaInfo.type === PopUpMediaInfo.BAR_CHART)
                {
                    showElement(barChart);
                    barSeries.styleName = mediaInfo.chartSeriesStyleName;
                    barChart.dataProvider = chartData;
                }
                else if (mediaInfo.type === PopUpMediaInfo.COLUMN_CHART)
                {
                    showElement(columnChart);
                    columnSeries.styleName = mediaInfo.chartSeriesStyleName;
                    columnChart.dataProvider = chartData;
                }
                else if (mediaInfo.type === PopUpMediaInfo.LINE_CHART)
                {
                    showElement(lineChart);
                    lineSeries.styleName = mediaInfo.chartSeriesStyleName;
                    lineChart.dataProvider = chartData;
                }
                else if (mediaInfo.type === PopUpMediaInfo.PIE_CHART)
                {
                    showElement(pieChart);
                    pieSeries.styleName = mediaInfo.chartSeriesStyleName;
                    pieChart.dataProvider = chartData;
                }
            }

            private function showOrHideCount():void
            {
                if (hostComponent.popUpMediaInfos && hostComponent.popUpMediaInfos.length > 1)
                {
                    showElement(countLabel);
                    countLabel.text = "(" + (hostComponent.activeIndex + 1) + " "
                        + resourceManager.getString("ESRIMessages", "attributeInspectorOf")
                        + " " + hostComponent.popUpMediaInfos.length + ")";
                }
                else
                {
                    hideElement(countLabel);
                }
            }

            private function insertStats(text:String):String
            {
                var sumKeywordRE:RegExp = /{=SUM}/;
                var minKeywordRE:RegExp = /{=MIN}/;
                var maxKeywordRE:RegExp = /{=MAX}/;
                var countKeywordRE:RegExp = /{=COUNT}/;
                var averageKeywordRE:RegExp = /{=AVERAGE}/;

                text = insertStatistic(text, sumKeywordRE, hostComponent.sum);
                text = insertStatistic(text, minKeywordRE, hostComponent.min);
                text = insertStatistic(text, maxKeywordRE, hostComponent.max);
                text = insertStatistic(text, countKeywordRE, hostComponent.count);
                text = insertStatistic(text, averageKeywordRE, hostComponent.average);

                return text;
            }

            private function insertStatistic(source:String, keyword:RegExp, insertionValue:Number):String
            {
                var finalInsertionValue:String = (hostComponent.precision > -1) ? numberFormatter.format(insertionValue) : insertionValue.toString();
                return source.replace(keyword, finalInsertionValue);
            }
        ]]>
    </fx:Script>

    <fx:Declarations>
        <s:NumberFormatter id="numberFormatter" fractionalDigits="{hostComponent.precision}"/>
    </fx:Declarations>

    <s:layout>
        <s:VerticalLayout/>
    </s:layout>

    <s:Label id="titleLabel"
             width="100%"
             fontWeight="bold"/>

    <s:HGroup width="100%" height="100%"
              gap="2"
              verticalAlign="middle">
        <s:Button id="previousButton"
                  height="100%"
                  skinClass="com.esri.ags.skins.supportClasses.PopUpMediaBrowserPreviousButtonSkin"/>

        <s:Group id="chartGroup"
                 width="100%" height="100%">
            <s:Rect id="borderRect"
                    left="0" right="0" top="0" bottom="0">
                <s:stroke>
                    <s:SolidColorStroke id="borderRectSymbol"
                                        color="black"
                                        weight="1"/>
                </s:stroke>
            </s:Rect>

            <mx:BarChart id="barChart"
                         left="1" right="1" top="1" bottom="1"
                         showDataTips="true">
                <mx:series>
                    <mx:BarSeries id="barSeries" xField="value"/>
                </mx:series>
                <mx:verticalAxis>
                    <mx:CategoryAxis id="barAxis" categoryField="name"/>
                </mx:verticalAxis>
                <mx:verticalAxisRenderers>
                    <mx:AxisRenderer axis="{barAxis}" showLabels="false"/>
                </mx:verticalAxisRenderers>
            </mx:BarChart>

            <mx:ColumnChart id="columnChart"
                            left="1" right="1" top="1" bottom="1"
                            showDataTips="true">
                <mx:series>
                    <mx:ColumnSeries id="columnSeries" yField="value"/>
                </mx:series>
                <mx:horizontalAxis>
                    <mx:CategoryAxis id="columnAxis" categoryField="name"/>
                </mx:horizontalAxis>
                <mx:horizontalAxisRenderers>
                    <mx:AxisRenderer axis="{columnAxis}" showLabels="false"/>
                </mx:horizontalAxisRenderers>
            </mx:ColumnChart>

            <mx:LineChart id="lineChart"
                          left="1" right="1" top="1" bottom="1"
                          showDataTips="true">
                <mx:series>
                    <mx:LineSeries id="lineSeries" yField="value"/>
                </mx:series>
                <mx:horizontalAxis>
                    <mx:CategoryAxis id="lineAxis" categoryField="name"/>
                </mx:horizontalAxis>
                <mx:horizontalAxisRenderers>
                    <mx:AxisRenderer axis="{lineAxis}" showLabels="false"/>
                </mx:horizontalAxisRenderers>
            </mx:LineChart>

            <mx:PieChart id="pieChart"
                         left="1" right="1" top="1" bottom="1"
                         showDataTips="true">
                <mx:series>
                    <mx:PieSeries id="pieSeries"
                                  field="value"
                                  labelField="name"
                                  nameField="name"/>
                </mx:series>
            </mx:PieChart>
        </s:Group>

        <s:Button id="nextButton"
                  height="100%"
                  skinClass="com.esri.ags.skins.supportClasses.PopUpMediaBrowserNextButtonSkin"/>
    </s:HGroup>

    <s:Label id="countLabel"
             width="100%"
             textAlign="end"/>

    <s:Label id="captionLabel"
             width="100%"
             fontStyle="italic"/>

</s:SparkSkin>
